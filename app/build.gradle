apply plugin: 'com.android.application'
apply plugin: 'androidx.navigation.safeargs'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 31
    buildToolsVersion '31.0.0'
    defaultConfig {
        applicationId "com.thewizrd.simpleweather"
        // Specifies the fully-qualified class name of the test instrumentation runner.
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        minSdkVersion 21
        targetSdkVersion 30
        // NOTE: Version Code Format (TargetSDK, Version Name, Build Number, Variant Code (Android: 00, WearOS: 01)
        versionCode 305200300
        versionName "5.2.0"

        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "stagGeneratedPackageName": "com.thewizrd.simpleweather.stag.generated"
                ]
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [allowBackup: false]
        }
        release {
            minifyEnabled true
            shrinkResources true
            crunchPngs true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [allowBackup: true]
        }
    }

    buildFeatures {
        dataBinding = true
        viewBinding = true
    }

    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true
        // Sets Java compatibility to Java 8
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    sourceSets {
        // Workaround for binding class errors
        fullgmsDebug {
            java.srcDirs += 'build/generated/data_binding_base_class_source_out/fullgmsDebug/out'
        }
        nongmsDebug {
            java.srcDirs += 'build/generated/data_binding_base_class_source_out/nongmsDebug/out'
        }

        main {
            manifest.srcFile 'src/main/AndroidManifest.xml'
        }
        fullgms {
            manifest.srcFile 'src/fullgms/AndroidManifest.xml'
        }
        nongms {
            manifest.srcFile 'src/main/AndroidManifest.xml'
        }
    }

    flavorDimensions 'gms'
    productFlavors {
        nongms {
            dimension 'gms'
            applicationIdSuffix ".foss"
            versionNameSuffix "-nongms"
            buildConfigField "boolean", "IS_NONGMS", "true"
        }
        fullgms {
            dimension 'gms'
            // Read the API key from ./secure.properties into R.string.maps_api_key
            def secureProps = new Properties()
            if (file("../secure.properties").exists()) {
                file("../secure.properties")?.withInputStream { secureProps.load(it) }
            }
            resValue "string", "maps_api_key", (secureProps.getProperty("MAPS_API_KEY") ?: "")
            buildConfigField "boolean", "IS_NONGMS", "false"
        }
    }
}

dependencies {
    implementation project(":shared_resources")
    fullgmsImplementation project(":extras")

    // Unit Testing
    androidTestImplementation 'androidx.test:core:1.4.0'

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'

    // Assertions
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.ext:truth:1.4.0'
    androidTestImplementation 'com.google.truth:truth:1.1.3'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2'

    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.core:core-splashscreen:1.0.0-alpha01'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.fragment:fragment-ktx:1.3.6'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.arch.core:core-runtime:2.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.annotation:annotation:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.palette:palette-ktx:1.0.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.paging:paging-runtime-ktx:3.0.1'
    implementation 'androidx.work:work-runtime-ktx:2.6.0'

    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'

    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'

    implementation 'androidx.room:room-runtime:2.3.0'
    kapt 'androidx.room:room-compiler:2.3.0'

    implementation 'com.squareup.okhttp3:okhttp:4.9.1'
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    kapt 'com.github.bumptech.glide:compiler:4.12.0'
    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation 'com.google.code.gson:gson:2.8.8'
    implementation 'com.ibm.icu:icu4j:69.1'
    implementation 'com.github.skydoves:colorpickerview:2.2.3'

    implementation("com.vimeo.stag:stag-library:2.6.0") {
        exclude group: "org.jetbrains", module: "annotations-java5"
    }
    kapt("com.vimeo.stag:stag-library-compiler:2.6.0") {
        exclude group: "org.jetbrains", module: "annotations-java5"
    }

    fullgmsImplementation 'com.revenuecat.purchases:purchases:4.3.1'

    // Firebase
    fullgmsImplementation platform('com.google.firebase:firebase-bom:28.4.1')
    fullgmsImplementation 'com.google.firebase:firebase-analytics'
    fullgmsImplementation 'com.google.firebase:firebase-crashlytics'
    fullgmsImplementation 'com.google.firebase:firebase-auth'
    fullgmsImplementation 'com.google.firebase:firebase-firestore'
    fullgmsImplementation 'com.google.firebase:firebase-messaging'
    fullgmsImplementation 'com.google.firebase:firebase-storage'
    fullgmsImplementation 'com.google.firebase:firebase-database'
    fullgmsImplementation 'com.google.firebase:firebase-config'
    fullgmsImplementation 'com.google.firebase:firebase-perf'

    fullgmsImplementation 'com.google.android.gms:play-services-base:17.6.0'
    fullgmsImplementation 'com.google.android.gms:play-services-location:18.0.0'
    fullgmsImplementation 'com.google.android.gms:play-services-maps:17.0.1'
    fullgmsImplementation 'com.google.android.gms:play-services-wearable:17.1.0'
    fullgmsImplementation 'com.google.android.play:core:1.10.1'
    fullgmsImplementation 'com.google.android.play:core-ktx:1.8.1'
    fullgmsImplementation 'com.google.android.libraries.places:places:2.4.0'
    fullgmsImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.5.2'

    nongmsImplementation 'org.osmdroid:osmdroid-android:6.1.11'
}

if (getGradle().getStartParameter().getTaskRequests().toString().contains("Fullgms")) {
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'com.google.firebase.crashlytics'
    apply plugin: 'com.google.firebase.firebase-perf'
}