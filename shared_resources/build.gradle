apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.3'

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation": "$projectDir/schemas".toString(),
                        "room.incremental": "true",
                        "room.expandProjection": "true",
                        "stagGeneratedPackageName": "com.thewizrd.shared_resources.stag.generated"
                ]
            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled true
        }
    }

    buildFeatures {
        dataBinding = true
        viewBinding = true
    }

    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true
        // Sets Java compatibility to Java 8
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }

    sourceSets {
        androidTest {
            // Adds exported schema location as test app assets.
            assets.srcDirs += files("$projectDir/schemas".toString())
        }
    }

    flavorDimensions 'gms'
    productFlavors {
        nongms {
            dimension 'gms'
            buildConfigField "boolean", "IS_NONGMS", "true"
        }
        fullgms {
            dimension 'gms'
            buildConfigField "boolean", "IS_NONGMS", "false"
            externalNativeBuild {
                ndkBuild {
                    arguments "FULL_GMS_VERSION=true"
                }
            }
        }
    }
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    // Unit Testing
    androidTestImplementation 'androidx.test:core:1.4.0'

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'

    // Assertions
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.ext:truth:1.4.0'
    androidTestImplementation 'com.google.truth:truth:1.1.3'

    // Kotlin
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.5.21'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.1'

    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.arch.core:core-runtime:2.1.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.annotation:annotation:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.palette:palette-ktx:1.0.0'
    implementation 'androidx.paging:paging-runtime-ktx:3.0.1'

    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    implementation 'androidx.room:room-runtime:2.3.0'
    kapt 'androidx.room:room-compiler:2.3.0'
    implementation 'androidx.room:room-ktx:2.3.0'
    // Room migration testing
    androidTestImplementation "androidx.room:room-testing:2.3.0"

    implementation 'com.squareup.okhttp3:okhttp:4.9.0'
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.google.code.gson:gson:2.8.7'
    implementation 'com.ibm.icu:icu4j:69.1'

    implementation("com.vimeo.stag:stag-library:2.6.0") {
        exclude group: "org.jetbrains", module: "annotations-java5"
    }
    kapt("com.vimeo.stag:stag-library-compiler:2.6.0") {
        exclude group: "org.jetbrains", module: "annotations-java5"
    }

    fullgmsImplementation 'com.google.android.gms:play-services-auth:19.2.0'
    fullgmsImplementation 'com.google.android.gms:play-services-base:17.6.0'
    fullgmsImplementation 'com.google.android.gms:play-services-tasks:17.2.1'
    fullgmsImplementation 'com.google.android.gms:play-services-wearable:17.1.0'
    fullgmsImplementation 'com.google.android.libraries.places:places:2.4.0'
    fullgmsImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.5.1'

    // Firebase
    fullgmsImplementation platform('com.google.firebase:firebase-bom:28.1.0')
    fullgmsImplementation 'com.google.firebase:firebase-analytics'
    fullgmsImplementation 'com.google.firebase:firebase-crashlytics'
    fullgmsImplementation 'com.google.firebase:firebase-auth'
    fullgmsImplementation 'com.google.firebase:firebase-firestore'
    fullgmsImplementation 'com.google.firebase:firebase-storage'
    fullgmsImplementation 'com.google.firebase:firebase-database'
    fullgmsImplementation 'com.google.firebase:firebase-config'
}

task keyCheck(type:Exec) {
    workingDir '../'
    commandLine 'cmd', '/c', 'keycheck.bat'

    doLast {
        execResult.assertNormalExitValue()
    }
}

android.libraryVariants.all { variant ->
    def name = variant.flavorName
    if (name.contains('fullgms')) {
        variant.preBuild.dependsOn keyCheck
    }
}